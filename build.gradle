plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.fonseca.algashop'
version = '0.0.1-SNAPSHOT'
description = 'Billing Microservice'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	mockitoAgent
}

repositories {
	mavenCentral()
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-h2console'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'

	implementation 'commons-validator:commons-validator:1.9.0'
	implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
	implementation 'io.hypersistence:hypersistence-tsid:2.1.3'
	implementation 'org.modelmapper:modelmapper:3.2.4'
	implementation 'org.apache.commons:commons-lang3:3.18.0'
	testImplementation 'org.assertj:assertj-core:3.27.3'

	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	mockitoAgent('org.mockito:mockito-core:5.14.2') {transitive = false}
}

tasks.named('test') {
	useJUnitPlatform()
}

def mockitoJvmArgs = [
		"-javaagent:${configurations.mockitoAgent.asPath}",
		"-Xshare:off" // Desabilita Class Data Sharing para evitar warning de bootstrap classpath
]

tasks.register('integrationTest', Test) {
	description = 'Run integrationTest.'
	group = 'verification'
	jvmArgs += mockitoJvmArgs
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
	shouldRunAfter test
	useJUnitPlatform()
	testLogging {
		events('passed')
	}

	filter {
		includeTestsMatching "*IT"
		excludeTestsMatching "*Test"
	}
}
